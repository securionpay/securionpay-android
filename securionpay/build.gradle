buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.kezong:fat-aar:1.3.6'
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 4
        versionName rootProject.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            reports {
                ignoreFailures = true
                junitXml.enabled = true
            }
        }
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        debug {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
            buildConfigField("String", "API_URL", "\"https://api.securionpay.com\"")
            buildConfigField("String", "BACKOFFICE_URL", "\"https://securionpay.com\"")

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
            buildConfigField("String", "API_URL", "\"https://api.securionpay.com\"")
            buildConfigField("String", "BACKOFFICE_URL", "\"https://securionpay.com\"")

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'com.kezong.fat-aar'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.securionpay'
                artifactId = 'securionpay-android'
                version = rootProject.version

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    dependencies {
        //Android
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'androidx.core:core-ktx:1.3.1'
        implementation 'com.google.android.material:material:1.0.0'

        //NSoftware
        //        debugImplementation fileTree(dir: "../ipworks_debug", include: ["*.aar"])
        //        implementation fileTree(dir: "../ipworks_debug", include: ["*.aar"])
        debugEmbed project(path: ':ipworks_debug', configuration: 'default')
        releaseEmbed project(path: ':ipworks', configuration: 'default')

        //Retrofit
        implementation 'com.squareup.retrofit2:retrofit:2.9.0'
        implementation 'com.google.code.gson:gson:2.8.5'
        implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

        //Coroutines
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'

        //External
        implementation 'com.alimuzaffar.lib:pinentryedittext:2.0.6'
        implementation group: 'com.apachat', name: 'loadingbutton-android', version: '1.0.11'

        //Test
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
        testImplementation 'org.apache.directory.studio:org.apache.commons.codec:1.8'
        testImplementation "androidx.arch.core:core-testing:2.1.0"
        testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2"
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
}
